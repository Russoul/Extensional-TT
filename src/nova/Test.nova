let test0 : ℕ
          ≔ Z

let test1 : ℕ ⨯ ℕ ⨯ ℕ
          ≔ Z , S Z , S (S Z)

let test4 : (A : 𝕌) ⨯ A
          ≔ ℕ , Z

let test31 : Z ≡ ? ∈ ℕ
           ≔ Refl

define sym : {A : 𝕌} → {a₀ a₁ : A} → (a₀ ≡ a₁ ∈ A) → (a₁ ≡ a₀ ∈ A)
           ≔ {A a₀ a₁} ↦ a ↦ J ? ? (x. e. x ≡ a₀ ∈ A) Refl ? a

define fun-eta : (A : 𝕌) → (B : A → 𝕌) → (f : (x : A) → B x) → ((x ↦ f x) ≡ f ∈ ((x : A) → B x))
               ≔ A B f ↦ Π-η f

define fun-ext : {A : 𝕌} → {B : A → 𝕌} → {f g : (x : A) → B x} → (p : (x : A) → f x ≡ g x ∈ B x) → f ≡ g ∈ ((x : A) → B x)
               ≔ {A B f g} ↦ p ↦ J ? ? (α. h. α ≡ g ∈ ?) (J ? ? (α. h. ?(A , B , f , g , p) ≡ α ∈ ?) (Π⁼ (x. ?) (x. ?) (x. p x)) ? (Π-η ?)) ? (Π-η ?)

define happly' : (A : 𝕌) → (B : 𝕌) → (f : A → B) → (g : A → B) → (p : f ≡ g ∈ ?) → (x : A) → f x ≡ g x ∈ ?
               ≔ A B f g p ↦ x ↦ J ? ? (α.h. f x ≡ α x ∈ ?) Refl ? p

define happly : (A : 𝕌) → (B : A → 𝕌) → (f : (x : A) → B x) → (g : (x : A) → B x) → (p : f ≡ g ∈ ?) → (x : A) → f x ≡ g x ∈ ?
              ≔ A B f g p x ↦ J ? ? (α.h. f x ≡ α x ∈ ?) Refl ? p

define cong : {A B : 𝕌} → (f : A → B) → {a₀ a₁ : A} → (a : a₀ ≡ a₁ ∈ ?) → f a₀ ≡ f a₁ ∈ ?
            ≔ {A B} ↦ f ↦ {a₀ a₁} ↦ a ↦ J ? ? (x. h. f a₀ ≡ f x ∈ ?) Refl ? a

define transp : (A : 𝕌) → (B : A → 𝕌) → {a₀ a₁ : A} → (a : a₀ ≡ a₁ ∈ ?) → B a₀ → B a₁
              ≔ A B ↦ {a₀ a₁} ↦ a ↦ t ↦ J ? ? (α. h. B α) t ? a


let pred' : ℕ → ℕ
          ≔ x ↦ ℕ-elim (x. ℕ) Z (x.h. x) x

define pred'-S : (x : ℕ) → pred' (S x) ≡ x ∈ ℕ
               ≔ x ↦ tac
                   unfold ☐ ≡ _ ∈ _;
                   trivial;

define S-cong-inv-H0 : {x y : ℕ} → (e : pred' (S x) ≡ pred' (S y) ∈ ?) → x ≡ y ∈ ?
                     ≔ {x y} ↦ tac
                          rewrite⁻¹ ((x : ☐ ≡ _ ∈ _) → _) (pred'-S ?);
                          rewrite⁻¹ ((x : _ ≡ ☐ ∈ _) → _) (pred'-S ?);
                          exact id;

define S-cong-inv : {x y : ℕ} → (e : S x ≡ S y ∈ ?) → x ≡ y ∈ ?
                  ≔ {x y} ↦ e ↦ S-cong-inv-H0 (cong pred' e)

let Vect : ℕ → 𝕌 → 𝕌
         ≔ n A ↦ ℕ-elim (x. 𝕌) 𝟙 (x.H. A ⨯ H) n

let nil : {A : 𝕌} → Vect Z A
        ≔ {A} ↦ tac unfold ☐;
                    exact tt;

let Vect-Z : {A : 𝕌} → Vect Z A ≡ 𝟙 ∈ 𝕌
               ≔ {A} ↦ tac
                     unfold ☐ ≡ _ ∈ _;
                     trivial;

let Vect-S : {A : 𝕌} → {n : ℕ} → Vect (S n) A ≡ A ⨯ Vect n A ∈ 𝕌
               ≔ {A n} ↦ tac
                     unfold ☐ ≡ _ ∈ _;
                     unfold _ ≡ ((x : _) ⨯ ☐ _ _) ∈ _;
                     trivial;

define nil-unique : {A : 𝕌} → (xs : Vect Z A) → xs ≡ nil {A} ∈ Vect Z A
                  ≔
                   {A} ↦ tac
                      rewrite⁻¹ ((x : ☐) → _) Vect-Z;
                      rewrite⁻¹ ((x : _) → _ ≡ _ ∈ ☐) Vect-Z;
                      exact (xs ↦ 𝟙⁼ ? ?);


let _::_ : {A : 𝕌} → {n : ℕ} → A → Vect n A → Vect (S n) A
         ≔ {A n} ↦ x xs ↦ tac
             rewrite⁻¹ ☐ Vect-S;
             exact x , xs;

define prod-η : {A B : 𝕌} → (p : A ⨯ B) → (p .π₁ , p .π₂) ≡ p ∈ ?
              ≔ {A B} ↦ p ↦ Σ-η p


define Vect-elim-H : {A : 𝕌}
                   → (P : (n : ℕ) → Vect n A → 𝕌)
                   → (n : ℕ)
                   → (xs : A ⨯ Vect n A)
                   → P (S n) (xs .π₁ :: xs .π₂)
                   → P (S n) (tac rewrite⁻¹ ☐ Vect-S; exact (xs .π₁ , xs .π₂);)
                   ≔ {A} ↦ P n xs ↦ tac
                       unfold P _ ☐ → _;
                       exact id;

let Vect-elim : {A : 𝕌}
              → {n : ℕ}
              → (P : (n : ℕ) → Vect n A → 𝕌)
              → (nil : P Z nil)
              → (cons : (n : ℕ) → (x : A) → (xs : Vect n A) → P n xs → P (S n) (x :: xs))
              → (xs : Vect n A)
              → P n xs
              ≔ {A n} ↦ P nil cons xs ↦ ℕ-elim (n. (xs : Vect n A) → P n xs)
                  (tac
                       rewrite⁻¹ (_ → _ ☐) (nil-unique xs);
                       exact (xs ↦ nil);
                  )
                  -- (n : ℕ) (h : (xs : Vect n A) → P n xs) ⊦ s : (xs : A ⨯ Vect n A) → P (S n) (xs.π₁, xs.π₂)
                  -- xs.π₁ : A
                  -- xs.π₂ : Vect n A
                  -- h xs.π₂ : P n xs.π₂
                  -- cons n xs.π₁ xs.π₂ (h xs.π₂) : P (S n) (cons xs.π₁ xs.π₂)
                  (n. h. tac
                    rewrite⁻¹ (☐ → _) Vect-S;
                    rewrite (_ → _ ☐) (Σ-η xs);
                    exact (xs ↦ Vect-elim-H P n xs (cons n (xs .π₁) (xs .π₂) (h (xs .π₂))));
                  )
                  n xs

define Vect-elim-nil : {A : 𝕌}
                     → (P : (n : ℕ) → Vect n A → 𝕌)
                     → (onNil : P Z nil)
                     → (onCons : (n : ℕ) → (x : A) → (xs : Vect n A) → P n xs → P (S n) (x :: xs))
                     → Vect-elim P onNil onCons nil ≡ onNil ∈ ?
                     ≔ {A} ↦ P onNil onCons ↦ tac
                          unfold ☐ ≡ _ ∈ _;
                          trivial;

define Vect-elim-cons : {A : 𝕌}
                      → {n : ℕ}
                      → (P : (n : ℕ) → Vect n A → 𝕌)
                      → (onNil : P Z nil)
                      → (onCons : (n : ℕ) → (x : A) → (xs : Vect n A) → P n xs → P (S n) (x :: xs))
                      → (x : A)
                      → (xs : Vect n A)
                      → Vect-elim P onNil onCons (x :: xs) ≡ onCons n x xs (Vect-elim P onNil onCons xs) ∈ ?
                      ≔ {A n} ↦ P onNil onCons x xs ↦ tac
                          unfold ☐ ≡ _ ∈ _;
                          unfold ☐ ≡ _ ∈ _;
                          unfold onCons _ _ ☐ _ ≡ _ ∈ _;
                          unfold onCons _ ☐ _ _ ≡ _ ∈ _;
                          unfold onCons _ _ _ (ℕ-elim _ _ (xs ↦ ☐) n _) ≡ _ ∈ _;
                          unfold onCons _ _ _ (ℕ-elim _ _ _ _ ☐) ≡ _ ∈ _;
                          unfold _ ≡ onCons _ _ _ ☐ ∈ _;
                          unfold _ ≡ onCons _ _ _ (ℕ-elim _ _ (xs ↦ ☐) n _) ∈ _;
                          trivial;


-- 0   + y = y
-- S x + y = S (x + y)
let _+_ : ℕ → ℕ → ℕ
        ≔ x y ↦ ℕ-elim (z. ℕ) y (x. h. S h) x

define plus-left-Z : (x : ℕ) → Z + x ≡ x ∈ ℕ
                   ≔ x ↦ J ? ?
                           (α. h. α Z x ≡ x ∈ ℕ)
                           Refl
                           ?
                           (sym !_+_)

define plus-left-S : (t : ℕ) → (x : ℕ) → (S t + x) ≡ S (t + x) ∈ ℕ
                   ≔ t ↦ x ↦ J ? ?
                               (_@_. h. S t @ x ≡ S (t @ x) ∈ ℕ)
                               (cong (the (ℕ → ℕ) (x ↦ S x))
                               Refl
                               )
                               ?
                               (sym !_+_)


define plus-right-Z : (x : ℕ) → x + Z ≡ x ∈ ℕ
                    ≔ x ↦ ℕ-elim (x. x + Z ≡ x ∈ ℕ)
                                 (J ? ? (α. h. α ≡ Z ∈ ℕ) Refl ? (sym (plus-left-Z Z)))
                                 (x. h. J ? ? (α. h. α ≡ S x ∈ ℕ) (cong (x ↦ S x) h) ? (sym (plus-left-S x Z)))
                                 x


-- (x y : ℕ) (h : x + S y ≡ S (x + y) ∈ ℕ) ⊦ S x + S y ≡ S (S x + y)
--                                           S (x + S y) ≡ S (S x + y)
--                                           x + S y ≡ S x + y
--                                           x + S y ≡ S (x + y)
define plus-right-S : (x y : ℕ) → (x + S y) ≡ S (x + y) ∈ ℕ
                    ≔ x y ↦ ℕ-elim (x. (x + S y) ≡ S (x + y) ∈ ℕ)
                                   (tac rewrite⁻¹ (☐ ≡ _ ∈ _) (plus-left-Z (S y));
                                        exact (cong (the (ℕ → ℕ) (x ↦ S x)) (tac rewrite⁻¹ (_ ≡ ☐ ∈ _) (plus-left-Z y); trivial;));
                                   )
                                   (x. h. tac rewrite⁻¹ (☐ ≡ _ ∈ _) (plus-left-S ? ?);
                                              exact cong (the (ℕ → ℕ) (x ↦ S x)) (tac rewrite⁻¹ (_ ≡ ☐ ∈ _) (plus-left-S ? ?); exact h;);
                                   )
                                   x


define plus-commut : (x y : ℕ) → x + y ≡ y + x ∈ ℕ
                   ≔ x y ↦
                      ℕ-elim (x. (y : ℕ) → x + y ≡ y + x ∈ ℕ)
                             (y ↦ tac
                                rewrite⁻¹ (☐ ≡ _ ∈ _) (plus-left-Z ?);
                                rewrite⁻¹ (_ ≡ ☐ ∈ _) (plus-right-Z ?);
                                trivial;
                             )
                             (x. h. y ↦ tac
                               rewrite⁻¹ (☐ ≡ _ ∈ _) (plus-left-S ? ?);
                               rewrite⁻¹ (_ ≡ ☐ ∈ _) (plus-right-S ? ?);
                               exact (cong (x ↦ S x) (h ?));
                             )
                             x y

-- 0 * y = 0
-- (1 + x) * y = y + x * y
let _*_ : ℕ → ℕ → ℕ
        ≔ x y ↦ ℕ-elim (x. ℕ) Z (x. h. y + h) x


define mult-left-Z : (x : ℕ) → Z * x ≡ Z ∈ ?
                    ≔ x ↦ tac
                        unfold ☐ ≡ _ ∈ _;
                        trivial;

define mult-right-Z : (x : ℕ) → x * Z ≡ Z ∈ ?
                    ≔ x ↦ tac
                        unfold ☐ ≡ _ ∈ _;
                        rewrite⁻¹ (ℕ-elim _ _ ☐ _ ≡ _ ∈ _) (plus-left-Z ?);
                        exact ℕ-elim (x. ℕ-elim (x. ℕ) Z (x.h. h) x ≡ Z ∈ ℕ) (tac trivial;) (x. h. tac exact h;) x;


define mult-left-S : (t : ℕ) → (x : ℕ) → (S t * x) ≡ (x + t * x) ∈ ?
                   ≔ t x ↦ tac
                       unfold ☐ ≡ _ ∈ _;
                       unfold _ ≡ _ + ☐ ∈ _;
                       trivial;


define Nat-eta : (A : ℕ → 𝕌)
               → (f : (x : ℕ) → A x)
               → (z : A Z)
               → (s : (x : ℕ) → A x → A (S x))
               → (f Z ≡ z ∈ ?)
               → ((x : ℕ) → f (S x) ≡ s x (f x) ∈ ?)
               → (f ≡ (x ↦ ℕ-elim (x. A x) z (x. h. s x h) x) ∈ ?)
               ≔ A f z s c0 c1 ↦ fun-ext (x ↦ ℕ-elim (x. f x ≡ ℕ-elim (x. A x) z (x.h. s x h) x ∈ A x) c0 (x.h. tac rewrite (_ ≡ _ ☐ ∈ _) h; exact c1 x;) x)


define trans : {A : 𝕌}
             → {x y z : A}
             → (x ≡ y ∈ A)
             → (y ≡ z ∈ A)
             → (x ≡ z ∈ A)
             ≔ {A x y z} ↦ p q ↦
                 tac rewrite (_ ≡ ☐ ∈ _) q;
                     rewrite (_ ≡ ☐ ∈ _) p;
                     trivial;


-- x + y ≡ x + z → y ≡ z
define ℕ-left-cancel : (x y z : ℕ) → (x + y ≡ x + z ∈ ℕ) → (y ≡ z ∈ ℕ)
                     ≔ x y z p ↦ ℕ-elim (x. (x + y ≡ x + z ∈ ℕ) → (y ≡ z ∈ ℕ))
                     -- (x : ℕ) (y : ℕ) (z : ℕ) (p : _+_(·) x y ≡ _+_(·) x z ∈ ℕ)  ⊦ z : (_+_(·) 0 y ≡ _+_(·) 0 z ∈ ℕ) → (y ≡ z ∈ ℕ) (NoSolve)
                       (tac rewrite⁻¹ ((x : ☐ ≡ _ ∈ _) → _) (plus-left-Z ?);
                            rewrite⁻¹ ((x : _ ≡ ☐ ∈ _) → _) (plus-left-Z ?);
                            exact id;)
                     -- (x : ℕ) (y : ℕ) (z : ℕ) (p : _+_(·) x y ≡ _+_(·) x z ∈ ℕ) (x : ℕ) (h : (x + y ≡ x + z ∈ ℕ) → (y ≡ z ∈ ℕ))  ⊦
                     -- s : (S (x + y) ≡ S (x + z) ∈ ℕ) → (y ≡ z ∈ ℕ) (NoSolve)
                       (x.h. tac rewrite⁻¹ ((x : ☐ ≡ _ ∈ _) → _) (plus-left-S ? ?);
                                 rewrite⁻¹ ((x : _ ≡ ☐ ∈ _) → _) (plus-left-S ? ?);
                                 exact (t ↦ h (S-cong-inv t));
                       )
                       x p

define plus-assoc : (x y z : ℕ) → x + (y + z) ≡ (x + y) + z ∈ ℕ
                  ≔ x y z ↦ ℕ-elim (y. x + (y + z) ≡ (x + y) + z ∈ ℕ)
                      -- x + z = x + z
                      (tac rewrite⁻¹ (_ ☐ ≡ _ ∈ _) (plus-left-Z ?);
                           rewrite⁻¹ (_ ≡ _ ☐ _ ∈ _) (plus-right-Z ?);
                           exact Refl;
                      )
                      (y. H.
                        tac rewrite⁻¹ (_ ☐ ≡ _ ∈ _) (plus-left-S ? ?);
                            rewrite⁻¹ (☐ ≡ _ ∈ _) (plus-right-S ? ?);
                            rewrite⁻¹ (_ ≡ _ ☐ _ ∈ _) (plus-right-S ? ?);
                            rewrite⁻¹ (_ ≡ ☐ ∈ _) (plus-left-S ? ?);
                            exact cong (x ↦ S x) H;
                      )
                      y

-- x + y + z + y + z + y + x = x + x + y + y + y + z + z
-- Proof using a commutative-monad solver tactic
-- define test-nat-solver-0 : (x y z : ℕ) → x + y + z + y + z + y + x ≡ x + x + y + y + y + z + z ∈ ℕ
--                          ≔ x y z ↦ tac normalise-nat ☐ ≡ _ ∈ _; trivial;

-- [] ++ ys = ys
-- (x :: xs) ++ ys = x :: (xs ++ ys)

let _++_ : {A : 𝕌} → {m n : ℕ} → Vect m A → Vect n A → Vect (m + n) A
         ≔ {A m n} ↦ xs ys ↦ Vect-elim (m xs ↦ Vect (m + n) A)
            (tac rewrite⁻¹ (_ ☐ _) (plus-left-Z n); exact ys;)
            (m x xs h ↦ tac rewrite⁻¹ (_ ☐ _) (plus-left-S ? ?); rewrite⁻¹ ☐ Vect-S; exact x, h;)
            xs

define append-left-nil : {A : 𝕌} → {n : ℕ} → (xs : Vect n A) → nil ++ xs ≡ (tac rewrite⁻¹ (_ ☐ _) (plus-left-Z ?); exact xs;) ∈ ?
                       ≔ {A n} ↦ xs ↦
                            tac unfold ☐ ≡ _ ∈ _;
                                unfold ☐ ≡ _ ∈ _;
                                trivial;

define append-left-cons : {A : 𝕌} → {m n : ℕ} → (x : A) → (xs : Vect m A) → (ys : Vect n A) → ((x :: xs) ++ ys ≡ (tac rewrite⁻¹ (_ ☐ _) (plus-left-S ? ?); exact x :: (xs ++ ys);) ∈ ?)
                        ≔ {A m n} ↦ x xs ys ↦ tac
                            unfold ☐ ≡ _ ∈ _;
                            unfold ☐ ≡ _ ∈ _;
                            unfold ☐ ≡ _ ∈ _;
                            unfold _ ≡ ☐ ∈ _;
                            unfold ☐ , _ ≡ _ ∈ _;
                            unfold _ ≡ _ , ☐  ∈ _;
                            unfold _ ≡ _ , ☐ ∈ _;
                            unfold _ , ℕ-elim _ _ _ m ☐ ≡ _ ∈ _;
                            trivial;

-- xs ++ [] = xs

define append-right-nil : {A : 𝕌} → {n : ℕ} → (xs : Vect n A) → xs ++ nil ≡ (tac rewrite⁻¹ (_ ☐ _) (plus-right-Z ?); exact xs;) ∈ ?
                       ≔ {A n} ↦ xs ↦ Vect-elim (n xs ↦ xs ++ nil ≡ (tac rewrite⁻¹ (_ ☐ _) (plus-right-Z ?); exact xs;) ∈ ?)
                           (tac rewrite⁻¹ (☐ ≡ _ ∈ _) (append-left-nil nil); trivial;)
                           (n x xs h ↦ tac rewrite⁻¹ (☐ ≡ _ ∈ _) (append-left-cons ? ? ?); rewrite⁻¹ (_ ☐ ≡ _ ∈ _) h; rewrite⁻¹ (_ ☐ _ _ ≡ _ ∈ _) (plus-right-Z ?); trivial;)
                           xs

-- reverseH xs nil = xs
-- reverseH xs (y :: ys) = reverseH (y :: xs) ys

let reverseH : {A : 𝕌} → {m n : ℕ} → Vect m A → Vect n A → Vect (m + n) A
                ≔  {A m n} ↦ xs ys ↦ Vect-elim
                     (n ys ↦ (m : ℕ) → Vect m A → Vect (m + n) A)
                     (m xs ↦ tac rewrite⁻¹ (_ ☐ _) (plus-right-Z ?); exact xs;)
                     -- h (S m) (y :: xs) : Vect (S m + n)
                     (n y ys h m xs ↦ tac rewrite⁻¹ (_ ☐ _) (plus-right-S ? ?); rewrite (_ ☐ _) (plus-left-S ? ?); exact h ? (y :: xs);)
                     ys m xs


-- reverseH xs [] = xs

define reverseH-nil : {A : 𝕌} → {n : ℕ} → (xs : Vect n A) → reverseH xs nil ≡ (tac rewrite⁻¹ (_ ☐ _) (plus-right-Z ?); exact xs;) ∈ ?
                   ≔ {A n} ↦ xs ↦ tac unfold ☐ ≡ _ ∈ _; rewrite⁻¹ (☐ _ _ ≡ _ ∈ _) (Vect-elim-nil (n ↦ ys ↦ (m : ℕ) → Vect m A → Vect (m + n) A) ? ?); trivial;

-- reverseH xs (y :: ys) = reverse (y :: xs) ys

-- reverseH xs (y :: ys) : Vect (m + S n) A
-- reverseH (y :: xs) ys : Vect (S m + n) A

define reverseH-cons : {A : 𝕌} → {m n : ℕ} → (xs : Vect m A) → (y : A) → (ys : Vect n A) →
                         (tac rewrite (_ ☐ _) (plus-right-S ? ?); exact reverseH xs (y :: ys);)
                            ≡
                         (tac rewrite (_ ☐ _) (plus-left-S ? ?); exact reverseH (y :: xs) ys;)
                            ∈
                         Vect (S (m + n)) A

-- reverseH xs (y :: ys) = reverseH (y :: xs) ys

                     ≔ {A m n} ↦ xs y ys ↦ tac
                       unfold ☐ ≡ _ ∈ _;
                       unfold ☐ ≡ _ ∈ _;
                       unfold ☐ ≡ _ ∈ _;
                       unfold ℕ-elim _ _ (x ↦ ☐) _ _ _ _ ≡ _ ∈ _;
                       unfold ℕ-elim _ _ _ _ ☐ _ _ ≡ _ ∈ _;
                       unfold ℕ-elim _ _ _ _ _ _ (☐ :: _) ≡ _ ∈ _;
                       unfold _ ≡ ☐ ∈ _;
                       unfold _ ≡ ☐ ∈ _;
                       unfold _ ≡ ℕ-elim _ _ (x ↦ ☐) _ _ _ _ ∈ _;
                       trivial;


-- (_::_(·) {A} {n} y ys .π₂)

-- reverse : Vect m → Vect n A → Vect (m + n)
-- reverse xs [] = xs : Vect (m + 0) A
-- reverse xs (y :: ys) = reverse (y :: xs) ys : Vect (m + S n) A

let reverse : {A : 𝕌} → {n : ℕ} → Vect n A → Vect n A
            ≔ {A n} ↦ xs ↦ tac rewrite (_ ☐ _) (plus-left-Z ?); exact reverseH nil xs;

let _∘_ : {A B C : 𝕌} → (B → C) → (A → B) → (A → C)
        ≔ {A B C} ↦ g f x ↦ g (f x)

let _≅_ : 𝕌 → 𝕌 → 𝕌
        ≔ A B ↦ (f : A → B) ⨯ (g : B → A) ⨯ (g ∘ f ≡ id ∈ ?) ⨯ (f ∘ g ≡ id ∈ ?)

let-type Monoid
     ≔ (A : 𝕌)
         ⨯ (z : A)
         ⨯ (_+_ : A → A → A)
         ⨯ ((x : A) → z + x ≡ x ∈ A)
         ⨯ ((x : A) → x + z ≡ x ∈ A)
         ⨯ ((x y z : A) → x + (y + z) ≡ (x + y) + z ∈ A)

define Is-Commut-Monoid : 𝕌 → 𝕌
     ≔ A ↦ (z : A)
         ⨯ (_+_ : A → A → A)
         ⨯ ((x : A) → z + x ≡ x ∈ A)
         ⨯ ((x : A) → x + z ≡ x ∈ A)
         ⨯ ((x y z : A) → x + (y + z) ≡ (x + y) + z ∈ A)
         ⨯ ((x y : A) → x + y ≡ y + x ∈ A)

define-type Commut-Monoid
     ≔ (A : 𝕌) ⨯ Is-Commut-Monoid A

define ℕ-Monoid : Monoid
                ≔ tac unfold ☐; exact ℕ, Z, _+_ , plus-left-Z, plus-right-Z, plus-assoc;

define ℕ-Commut-Monoid : Commut-Monoid
                ≔ ℕ, Z, _+_ , plus-left-Z, plus-right-Z, plus-assoc, plus-commut

define test-commut-monoid-solver : (x y : ℕ) → x + Z + y ≡ y + Z + x ∈ ℕ
                                 ≔ x y ↦ tac
                                     normalise-comm-monoid (☐ ≡ _ ∈ _) (x y ⊦ x + 0 + y) ℕ-Commut-Monoid;
                                     normalise-comm-monoid (_ ≡ ☐ ∈ _) (x y ⊦ y + 0 + x) ℕ-Commut-Monoid;
                                     exact ?t;

let one : ℕ
        ≔ S Z

let two : ℕ
        ≔ one + one

let four : ℕ
   ≔ two * two
