let test0 : ℕ
          ≔ Z

assume test2 : ℕ → ℕ

assume test3 : (A : 𝕌) → A

let test31 : Z ≡ ? ∈ ℕ
           ≔ Refl

assume test4 : test0 ≡ Z ∈ ℕ

let id : (A : 𝕌) → A → A
       ≔ A ↦ x ↦ x

assume fun-eta : (A : 𝕌) → (B : A → 𝕌) → (f : (x : A) → B x) → (f ≡ (x ↦ f x) ∈ (x : A) → B x)

assume fun-ext : (A : 𝕌) → (B : A → 𝕌) → (f : (x : A) → B x) → (g : (x : A) → B x) → (p : (x : A) → f x ≡ g x ∈ B x) → f ≡ g ∈ (x : A) → B x

let sym : (A : 𝕌) → (a₀ : A) → (a₁ : A) → (a₀ ≡ a₁ ∈ A) → (a₁ ≡ a₀ ∈ A)
        ≔ A ↦ a₀ ↦ a₁ ↦ a ↦ J (x. e. x ≡ a₀ ∈ A) Refl a

let cong : (A : 𝕌) → (B : 𝕌) → (f : A → B) → (a₀ : A) → (a₁ : A) → (a : a₀ ≡ a₁ ∈ ?) → f a₀ ≡ f a₁ ∈ ?
         ≔ A ↦ B ↦ f ↦ a₀ ↦ a₁ ↦ a ↦ J (x. h. f a₀ ≡ f x ∈ ?) Refl a

let S-cong : (x : ℕ) → (y : ℕ) → (e : x ≡ y ∈ ?) → S x ≡ S y ∈ ?
         ≔ x ↦ y ↦ e ↦ J (z. h. S x ≡ S z ∈ ?) Refl e

let plus : ℕ → ℕ → ℕ
         ≔ x ↦ y ↦ ℕ-elim (x. ?) y (x. h. S h) x

let plus-left-Z : (x : ℕ) → plus Z x ≡ x ∈ ℕ
                ≔ x ↦ J (α. h. α Z x ≡ x ∈ ℕ)
                        (J (α. h. α x ≡ x ∈ ℕ)
                           (J (α. h. α ≡ x ∈ ℕ)
                              (J (α. h. α ≡ x ∈ ℕ) Refl (sym ? ? ? (ℕ-β-Z (x. ?) ? (x.h. ?))))
                              (sym ? ? ? (Π-β (y. ?) ?))
                           )
                           (sym ? ? ? (Π-β (x. ?) ?))
                        )
                        (sym ? ? ? !plus)

-- (x ↦ y ↦ ℕ-elim (x. ℕ) y (x.h. S h) x) (S t)
-- y ↦ ℕ-elim (x. ℕ) y (x.h. S h) (S t)
-- ? : (x ↦ y ↦ ℕ-elim (x. ℕ) y (x.h. S h) x) (S t) ≡ ?
-- ((z ↦ ?₀) ?₁ ≡ ?₀[?₁/z]) ~ ((x ↦ y ↦ ℕ-elim (x. ℕ) y (x.h. S h) x) (S t) ≡ ?₃)
let plus-left-S : (t : ℕ) → (x : ℕ) → plus (S t) x ≡ S (plus t x) ∈ ℕ
                ≔ t ↦ x ↦ J (α. h. α (S t) x ≡ ?(t, x) ∈ ℕ)
                            (J (α. h. α x ≡ ?(t, x) ∈ ℕ)
                               (J (α. h. α ≡ ?(t, x) ∈ ℕ)
                                  (J (α. h. α ≡ ?(t, x) ∈ ℕ)
                                     (S-cong ? ?
                                       (J (α.h. ?(t, x) ≡ α t x ∈ ℕ)
                                          (J (α.h. ?(t, x) ≡ α x ∈ ℕ)
                                             (J (α.h. ?(t, x) ≡ α ∈ ℕ)
                                                Refl
                                                (sym ? ? ? (Π-β (z. ?) ?))
                                             )
                                             (sym ? ? ? (Π-β (z. ?) ?))
                                          )
                                          (sym ? ? ? !plus)
                                       )
                                     )
                                     (sym ? ? ? (ℕ-β-S (x. ?) ? (x. h. ?) ?))
                                  )
                                  (sym ? ? ? (Π-β (z. ?) ?))
                               )
                               (sym ? ? ? (Π-β (z. ?) ?))
                            )
                            (sym ? ? ? !plus)
