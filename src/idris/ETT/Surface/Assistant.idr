module ETT.Surface.Assistant

-- Assistant given a (typed) signature Œ£
-- Provides a set of transformations over Œ£ which result in a new (typed) signature Œ£'

import Control.Monad.FailSt

import Data.Fin
import Data.Location

import public Text.Parser.Fork
import Text.Lexer
import Text.Parser.CharUtil

import Text.PrettyPrint.Prettyprinter.Render.Terminal
import Text.PrettyPrint.Prettyprinter

import ETT.Core.Language
import ETT.Core.Substitution
import ETT.Core.Conversion
import ETT.Core.Pretty
import ETT.Core.VarName

import ETT.Surface.Check
import ETT.Surface.Language
import ETT.Surface.Parser
import ETT.Surface.ParserUtil

||| Signature transformation
public export
data Transformation : Type where
  ||| Œ£ ‚áõ Œ£
  Id : Transformation
  ||| Œ£ (?Œì ctx) ‚áõ Œ£
  WkCtx : VarName -> Transformation
  ||| Œ£ (?Œì ctx) (?Œì ‚ä¶ ?A type) ‚áõ Œ£
  WkTypE : VarName -> VarName -> Transformation
  ||| Œ£ (?Œì ctx) (?Œì ‚ä¶ ?A type) (?Œì ‚ä¶ ?x : ?A) ‚áõ Œ£
  WkElem : VarName -> VarName -> VarName -> Transformation
  ||| Œ£ (?Œì ctx) (?Œì ‚ä¶ ?A type) (?Œì ‚ä¶ ?e : ?A) (?Œì ‚ä¶ ?x ‚âî ?e : ?A) ‚áõ Œ£
  WkLetElem : VarName -> VarName -> VarName -> VarName -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(Œµ/?Œì) ‚áí Œ£‚ÇÄ (?Œì ctx) Œ£‚ÇÅ
  InstCtxEmpty : VarName -> Transformation
  ||| Œ£‚ÇÄ (?Œì ctx) (?Œì ‚ä¶ ?A type) Œ£‚ÇÅ(?Œì (x : ?A) / ?Œì) ‚áí Œ£‚ÇÄ (?Œì ctx) Œ£‚ÇÅ
  InstCtxCons : (contextName : VarName) -> (newContextName : VarName) -> (typeName : VarName) -> (binderName : VarName) -> Transformation
  ||| Œ£‚ÇÄ (Œì ‚ä¶ ?X type) (Œì ‚ä¶ ?Y type) Œ£‚ÇÅ(?X ‚Üí ?Y / ?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypEExp : (solveMe : VarName) -> (dom : VarName) -> (cod : VarName) -> Transformation
  ||| Œ£‚ÇÄ (Œì ‚ä¶ ?X type) (Œì (x : ?X) ‚ä¶ ?Y type) Œ£‚ÇÅ((x : ?X) ‚Üí ?Y / ?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypEPi : (solveMe : VarName) -> (dom : VarName) -> (x : VarName) -> (cod : VarName) -> Transformation
  ||| Œ£‚ÇÄ (Œì (x : A) ‚ä¶ f : B) Œ£‚ÇÅ((x ‚Ü¶ f) / f) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ f : (x : A) ‚Üí B) Œ£‚ÇÅ
  InstElemLam : (solveMe : VarName) -> (newName : VarName) -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(‚Ñï/?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypENat : (solveMe : VarName) -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(ùïå/?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypEUniverse : (solveMe : VarName) -> Transformation
  ||| Œ£‚ÇÄ (x : ùïå) Œ£‚ÇÅ(El x/?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypEEl : (solveMe : VarName) -> (x : VarName) -> Transformation
  ||| Œ£‚ÇÄ (Œì ‚ä¶ ?A type) (Œì ‚ä¶ ?a‚ÇÄ : ?A) (Œì ‚ä¶ ?a‚ÇÅ : ?A) Œ£‚ÇÅ(?a‚ÇÄ ‚â° ?a‚ÇÅ ‚àà ?A / ?A) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ?A type) Œ£‚ÇÅ
  InstTypEEq : (solveMe : VarName) -> (typeName : VarName) -> (leftName : VarName) -> (rightName : VarName) -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(Œ≥‚ÇÄ.x.Œ≥‚ÇÅ.x / œá) ‚áõ Œ£‚ÇÄ (Œì‚ÇÄ (x : A) Œì‚ÇÅ ‚ä¶ œá : A) Œ£‚ÇÅ
  InstContextVar : (solveMe : VarName) -> (contextVar : VarName) -> Transformation
  RenameSigVar : Nat -> VarName -> Transformation
  RenameCtxVar : Nat -> Nat -> VarName -> Transformation
  ||| Œ£‚ÇÄ (Œì ‚ä¶ X type) (Œì (x : X) ‚ä¶ Y type) (Œì ‚ä¶ f : (x : X) ‚Üí Y) (Œì ‚ä¶ e : X) (Œì ‚ä¶ Y(e/x) = A type) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ x : A) Œ£‚ÇÅ
  InstPiElim : (solveMe, dom, x, cod, f, e, eq : VarName) -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(*/Œ±) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ [Œ±] A = A type) Œ£‚ÇÅ
  InstTyRefl : VarName -> Transformation
  ||| Œ£‚ÇÄ (Œì (x : A) ‚ä¶ Œ≤ : f x ‚â° g x ‚àà B) Œ£‚ÇÅ ‚áí Œ£‚ÇÄ (Œì ‚ä¶ Œ± : f ‚â° g ‚àà (x : A) ‚Üí B) Œ£‚ÇÅ
  FunExt : (alpha, x, beta : VarName) -> Transformation
  ||| Œ£ ‚áí Œ£ E
  DebugDropLast : Transformation
  ||| Œ£‚ÇÄ (Œì ‚ä¶ z : A(0/x)) (Œì (x : ‚Ñï) (h : A) ‚ä¶ s : A(S x / x)) Œ£‚ÇÅ(x. ‚Ñï-elim x.A z x.h.s x / Œ±) ‚áí Œ£‚ÇÄ (Œì (x : ‚Ñï) ‚ä¶ Œ± : A) Œ£‚ÇÅ
  InstNatElim : (alpha, z, s, h : VarName) -> Transformation
  InstNatZero : VarName -> Transformation
  InstNatSuc : VarName -> VarName -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(t/?) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ A type) Œ£‚ÇÅ
  InstSurfaceType : VarName -> SurfaceTerm -> Transformation
  ||| Œ£‚ÇÄ Œ£‚ÇÅ(t/?) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ ? : A) Œ£‚ÇÅ
  InstSurfaceElem : VarName -> SurfaceTerm -> Transformation

fromCheckM : CheckSt -> CheckM a -> M (a, CheckSt)
fromCheckM initial f = FailSt.do
  mapState (const ()) (const initial) $ FailSt.do
     x <- f
     st <- get
     return (x, st)

funTy : Elem -> Elem -> Elem
funTy a b = PiTy "_" a (ContextSubstElim b Wk)

||| Œì‚ÇÄ (x·µ¢ : A) Œì‚ÇÅ ‚ä¶ x·µ¢ : A
lookupContext : Context -> VarName -> Maybe (Elem, Elem)
lookupContext Empty x = Nothing
lookupContext (SignatureVarElim i) x = Nothing
lookupContext (Ext ctx x ty) y = FailSt.do
  case x == y of
    True => Just (ContextVarElim 0, ContextSubstElim ty Wk)
    False => do
      (t, ty) <- lookupContext ctx y
      Just (ContextSubstElim t Wk, ContextSubstElim ty Wk)

splitByVarName : Signature -> VarName -> M (Signature, SignatureEntry, Signature)
splitByVarName [<] x = throw "Can't find \{x} in the signature"
splitByVarName (sig :< (v, e)) x = FailSt.do
  case v == x of
    True => return (sig, e, [<])
    False => FailSt.do
      (sig0, entry, sig1) <- splitByVarName sig x
      return (sig0, entry, sig1 :< (v, e))

splitByIndex : Signature -> Nat -> M (Signature, VarName, SignatureEntry, Signature)
splitByIndex [<] x = throw "index out of bounds"
splitByIndex (sig :< (v, e)) 0 = FailSt.do
  return (sig, v, e, [<])
splitByIndex (sig :< (v, e)) (S k) = FailSt.do
  (sig0, x, entry, sig1) <- splitByIndex sig k
  return (sig0, x, entry, sig1 :< (v, e))

public export
compute : (target : Signature) -> Transformation -> M Signature
compute target Id = FailSt.do
  io $ putStrLn "Signature length: \{show $ length $ target}"
  io $ putStrLn (renderDocTerm !(prettySignature [<] target))
  return target
compute target (WkCtx ctxN) = return (target :< (ctxN, CtxEntry))
compute target (WkTypE ctxN typeN) = return (target :< (ctxN, CtxEntry) :< (typeN, TypeEntry Var))
compute target (WkElem ctxN typeN elemN) =
  -- Œ£ (Œì ctx) (Œì ‚ä¶ A type) Œì ‚ä¶ A type
  -- Œ£ (Œì ctx) (Œì ‚ä¶ A type) (Œì ‚ä¶ a : A)
  return $
    target :< (ctxN, CtxEntry) :< (typeN, TypeEntry Var) :< (elemN, ElemEntry (VarN 1) (SignatureVarElim 0 Id))
compute target (WkLetElem ctxN typeN elemN letN) =
  -- Œ£ (Œì ctx) (Œì ‚ä¶ A type) Œì ‚ä¶ A type
  -- Œ£ (Œì ctx) (Œì ‚ä¶ A type) (Œì ‚ä¶ a : A)
  return $
    target
      :<
    (ctxN, CtxEntry)
      :<
    (typeN, TypeEntry Var)
      :<
    (elemN, ElemEntry (VarN 1) (SignatureVarElim 0 Id))
      :<
    (letN, LetElemEntry (VarN 2) (SignatureVarElim 0 Id) (SignatureVarElim 1 Id))
compute target (InstCtxEmpty x) = FailSt.do
  -- Œ£‚ÇÄ (A ctx) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ ‚ä¶ Œ£‚ÇÅ(id, Œµ)
  (sig0, CtxEntry, sig1) <- splitByVarName target x
    | _ => throw "\{x} is not a 'ctx' entry"
  return (sig0 ++ subst sig1 (Ext Id (CtxEntryInstance Empty)))
compute target (InstCtxCons ctxN newCtxN typeN binderN) = FailSt.do
  -- Œ£‚ÇÄ (Œì ctx) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ctx) (Œì ‚ä¶ A type) ‚ä¶ Œ£‚ÇÅ(‚Üë¬≤, Œì (x : A))
  (sig0, CtxEntry, sig1) <- splitByVarName target ctxN
    | _ => throw "\{ctxN} is not a 'ctx' entry"
  return ((sig0 :< (newCtxN, CtxEntry) :< (typeN, TypeEntry Var))
            ++
          Signature.subst sig1 (Ext (WkN 2) (CtxEntryInstance $ Ext (VarN 1) binderN Var))
         )
compute target (InstTypEExp solveMe dom cod) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) (Œì(‚Üë) ‚ä¶ B type) ‚ä¶ Œ£‚ÇÅ(‚Üë¬≤, A‚ÇÅ ‚Üí B‚ÇÄ)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target solveMe
    | _ => throw "\{solveMe} is not a 'type' entry"
  return $ (sig0 :< (dom, TypeEntry ctx)) :< (cod, TypeEntry (subst ctx (the SubstSignature Wk)))
             ++
           Signature.subst sig1 (Ext (WkN 2)
             (TypeEntryInstance $ funTy (SignatureVarElim 1 Id) (SignatureVarElim 0 Id)))
compute target (InstTypEPi solveMe dom x cod) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) (Œì(‚Üë) (x : A) ‚ä¶ B type) ‚ä¶ Œ£‚ÇÅ(‚Üë¬≤, (x : A‚ÇÅ) ‚Üí B‚ÇÄ)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target solveMe
    | _ => throw "\{solveMe} is not a 'type' entry"
  return $ (sig0 :< (dom, TypeEntry ctx)) :< (cod, TypeEntry $ Ext (subst ctx (the SubstSignature Wk)) x (SignatureVarElim 0 Id))
             ++
           Signature.subst sig1
             (Ext (WkN 2)
                  (TypeEntryInstance $ PiTy x
                                        (SignatureVarElim 1 Id)
                                        (SignatureVarElim 0 Id)
                  )
             )
compute target (InstTypENat x) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ œá type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ ‚ä¶ Œ£‚ÇÅ(id, ‚Ñï)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target x
    | _ => throw "\{x} is not a 'type' entry"
  return (sig0 ++ subst sig1 (Ext Id (TypeEntryInstance NatTy)))
compute target (InstNatZero x) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ x : ‚Ñï) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ ‚ä¶ Œ£‚ÇÅ(id, 0)
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target x
    | _ => throw "\{x} is not an 'elem' entry"
  let NatTy = runSubst ty
    | _ => throw "Not ‚Ñï"
  return (sig0 ++ subst sig1 (Ext Id (ElemEntryInstance NatVal0)))
compute target (InstNatSuc x t) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ x : ‚Ñï) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ‚ä¶ t : ‚Ñï) ‚ä¶ Œ£‚ÇÅ(‚Üë, S t‚ÇÄ)
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target x
    | _ => throw "\{x} is not an 'elem' entry"
  let NatTy = runSubst ty
    | _ => throw "Not ‚Ñï"
  return (sig0 ++ [< (t, ElemEntry ctx NatTy)] ++ subst sig1 (Ext Wk (ElemEntryInstance (NatVal1 SigVar))))
compute target (InstTypEUniverse x) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ œá type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ ‚ä¶ Œ£‚ÇÅ(id, ùïå)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target x
    | _ => throw "\{x} is not a 'type' entry"
  return (sig0 ++ subst sig1 (Ext Id (TypeEntryInstance Universe)))
compute target (InstTypEEl solveN typeN) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ œá type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ‚ä¶ A : ùïå) ‚ä¶ Œ£‚ÇÅ(‚Üë, El A‚ÇÄ)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not a 'type' entry"
  return (sig0 :< (typeN, ElemEntry ctx Universe) ++ subst sig1 (Ext Wk (TypeEntryInstance (SignatureVarElim 0 Id))))
compute target (InstTypEEq solveN typeN leftN rightN) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì ‚ä¶ A type) (Œì(‚Üë) ‚ä¶ a‚ÇÄ : A) (Œì(‚Üë¬≤) ‚ä¶ a‚ÇÅ : A) ‚ä¶ Œ£‚ÇÅ(‚Üë¬≥, a‚ÇÄ ‚â° a‚ÇÅ ‚àà A)
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not a 'type' entry"
  let ctx' = subst ctx (the SubstSignature Wk)
  let ctx'' = subst ctx (the SubstSignature (Chain Wk Wk))
  let ctx''' = subst ctx (the SubstSignature (Chain (Chain Wk Wk) Wk))
  return $ (sig0 :< (typeN, TypeEntry ctx) :< (leftN, ElemEntry ctx' Var) :< (rightN, ElemEntry ctx'' (SignatureVarElim 1 Id)))
             ++
           Signature.subst sig1 (Ext (WkN 3) (TypeEntryInstance $ EqTy (SignatureVarElim 1 Id) (SignatureVarElim 0 Id) (SignatureVarElim 2 Id)))
compute target (InstElemLam solveN newN) = FailSt.do
  -- Œ£‚ÇÄ (Œì ‚ä¶ f : (x : A) ‚Üí B) ‚ä¶ Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì (x : A) ‚ä¶ f : B) ‚ä¶ Œ£‚ÇÅ(‚Üë, Œª x A(‚Üë) B(‚Üë) f)
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not an 'elem' entry"
  let PiTy x a b = runSubst ty
    | _ => throw "\{solveN} is not a Œ†-type"
  let ctx' = Ext ctx x a
  return $
    sig0 :< (newN, ElemEntry ctx' b)
      ++
    subst sig1 (Ext Wk (ElemEntryInstance $ PiVal x (SignatureSubstElim a Wk) (SignatureSubstElim b Wk) (SignatureVarElim 0 Id)))
compute target (InstContextVar solveN varN) = FailSt.do
  --  Œ£‚ÇÄ (Œì‚ÇÄ (x : A) Œì‚ÇÅ ‚ä¶ œá : A) ‚ä¶ Œ£‚ÇÅ
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not a 'elem' entry"
  let Just (sol, gotTy) = lookupContext ctx varN
    | Nothing => throw "Undefined context name: \{varN}"
  True <- conv ty gotTy
    | False => throw "Context variable's type doesn't match the expected type"
  return $
    sig0 ++ subst sig1 (Ext Id (ElemEntryInstance sol))
compute target (InstSurfaceType solveN surfaceTm) = FailSt.do
  -- œÉ : Œ£‚ÇÄ Œ£‚ÇÄ' ‚áí Œ£‚ÇÄ (Œì ‚ä¶ A type)
  --  Œ£‚ÇÄ Œ£‚ÇÄ' Œ£‚ÇÅ(‚Üë(Œ£‚ÇÄ), tm) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ A type) Œ£‚ÇÅ
  --
  (sig0, TypeEntry ctx, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not a 'elem' entry"
  (tm, MkCheckSt holes) <- fromCheckM (MkCheckSt [<]) $ checkElem sig0 ctx surfaceTm Universe
  return $
    sig0 ++ toSignature holes ++ subst sig1 (Ext (WkN (length holes)) (TypeEntryInstance tm))
compute target (InstSurfaceElem solveN surfaceTm) = FailSt.do
  --  Œ£‚ÇÄ (Œì ‚ä¶ x : A) ‚ä¶ Œ£‚ÇÅ
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target solveN
    | _ => throw "\{solveN} is not a 'elem' entry"
  (tm, MkCheckSt holes) <- fromCheckM (MkCheckSt [<]) $ checkElem sig0 ctx surfaceTm ty
  return $
    sig0 ++ toSignature holes ++ subst sig1 (Ext (WkN (length holes)) (ElemEntryInstance tm))
compute target (RenameSigVar i x) = FailSt.do
  (sig0, _, e, sig1) <- splitByIndex target i
  return (extend sig0 x e ++ sig1)
compute target (RenameCtxVar i j x) = FailSt.do
  (sig0, n, e, sig1) <- splitByIndex target i
  return (extend sig0 n !(renameEntry e j x) ++ sig1)
 where
  renameCtx : Context -> Nat -> VarName -> M Context
  renameCtx Empty i x = throw "index out of bounds"
  renameCtx (Ext ctx _ ty) 0 x = return $ Ext ctx x ty
  renameCtx (Ext ctx x' ty) (S i) x = return $ Ext !(renameCtx ctx i x) x' ty
  renameCtx (SignatureVarElim k) i x = throw "index out of bounds"

  renameEntry : SignatureEntry -> Nat -> VarName -> M SignatureEntry
  renameEntry CtxEntry j x = throw "Can't rename here"
  renameEntry (TypeEntry ctx) j x = return $ TypeEntry !(renameCtx ctx j x)
  renameEntry (ElemEntry ctx ty) j x = return $ ElemEntry !(renameCtx ctx j x) ty
  renameEntry (LetElemEntry ctx e ty) j x = return $ LetElemEntry !(renameCtx ctx j x) e ty
  renameEntry (EqTyEntry ctx a b) j x = return $ EqTyEntry !(renameCtx ctx j x) a b
compute target (InstPiElim n dom x cod f e eq) = FailSt.do
   -- Œ£‚ÇÄ ‚ä¶ Œì ctx
   -- Œ£‚ÇÄ Œì ‚ä¶ A type
   -- Œ£‚ÇÄ Œì ‚ä¶ t : A
   -- Œ£‚ÇÄ (Œì ‚ä¶ X type) (Œì (x : X) ‚ä¶ Y type) (Œì ‚ä¶ f : (x : X) ‚Üí Y) (Œì ‚ä¶ e : X) (Œì ‚ä¶ Y(e/x) = A type) Œ£‚ÇÅ(ap f x X Y e / t) ‚áí Œ£‚ÇÄ (Œì ‚ä¶ t : A) Œ£‚ÇÅ
   -- Œ£‚ÇÄ (Œì ‚ä¶ X type) (Œì(‚Üë) (x : œá‚ÇÄ) ‚ä¶ Y type) (Œì(‚Üë¬≤) ‚ä¶ f : (x : X‚ÇÅ) ‚Üí Y‚ÇÄ) (Œì(‚Üë¬≥) ‚ä¶ e : X‚ÇÇ) (Œì(‚Üë‚Å¥) ‚ä¶ Y‚ÇÇ(id, e‚ÇÄ) = A(‚Üë‚Å¥) type)
   --     Œ£‚ÇÅ(‚Üë‚Åµ, ap f‚ÇÇ x X‚ÇÑ Y‚ÇÉ e‚ÇÅ)
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target n
    | _ => throw "\{n} is not a 'elem' entry"
  let sig = extend (extend sig0 dom (TypeEntry ctx)) cod (TypeEntry (Ext (subst ctx Wk) x Var))
  let sig = extend sig f (ElemEntry (subst ctx (WkN 2)) (PiTy x (VarN 1) Var))
  let sig = extend sig e (ElemEntry (subst ctx (WkN 3)) (VarN 2))
  let sig = extend sig eq (EqTyEntry (subst ctx (WkN 4)) (SignatureVarElim 2 (Ext Id SigVar)) (SignatureSubstElim ty (WkN 4)))
  let sig = sig ++ subst sig1 (Ext (WkN 5) (ElemEntryInstance $ PiElim (SigVarN 2) x (VarN 4) (VarN 3) (SigVarN 1)))
  return sig
compute target (InstTyRefl n) = FailSt.do
  (sig0, EqTyEntry ctx a b, sig1) <- splitByVarName target n
    | _ => throw "\{n} is not a '_ = _ type' entry"
  True <- conv a b
    | _ => throw "\{n} is not a reflexive equality"
  return (sig0 ++ subst sig1 (Ext Id EqTyEntryInstance))
compute target (FunExt alpha x beta) = FailSt.do
  -- Œ£‚ÇÄ (Œì (x : A) ‚ä¶ Œ≤ : f x ‚â° g x ‚àà B) Œ£‚ÇÅ ‚áí Œ£‚ÇÄ (Œì ‚ä¶ Œ± : f ‚â° g ‚àà (x : A) ‚Üí B) Œ£‚ÇÅ
  -- Œ£‚ÇÄ (Œì (x : A) ‚ä¶ Œ≤ : ap f(‚Üë) A(‚Üë) B(‚Üë‚Å∫) x‚ÇÄ ‚â° g(‚Üë) x‚ÇÄ ‚àà B) Œ£‚ÇÅ(‚Üë, *)
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target alpha
    | _ => throw "\{alpha} is not an 'elem' entry"
  let EqTy lhs rhs ty = runSubst ty
    | _ => throw "Not an equality type"
  let PiTy v a b = runSubst ty
    | _ => throw "Not a function type"
  let ctx' = Ext ctx x a
  let sig = extend sig0 beta
             (ElemEntry ctx'
               (EqTy (PiElim (ContextSubstElim lhs Wk) v (ContextSubstElim a Wk) (ContextSubstElim b (Under Wk)) CtxVar)
                     (PiElim (ContextSubstElim rhs Wk) v (ContextSubstElim a Wk) (ContextSubstElim b (Under Wk)) CtxVar)
                     b
               )
             )
  return (sig ++ subst sig1 (Ext Wk (ElemEntryInstance EqVal)))
compute target (InstNatElim alpha z s h) = FailSt.do
  --   Œ£‚ÇÄ (Œì ‚ä¶ z : A(0/x)) (Œì (x : ‚Ñï) (h : A) ‚ä¶ s : A(S x / x)) Œ£‚ÇÅ(x. ‚Ñï-elim x.A z x.h.s x / Œ±) ‚áí Œ£‚ÇÄ (Œì (x : ‚Ñï) ‚ä¶ Œ± : A) Œ£‚ÇÅ
  --   Œ£‚ÇÄ (Œì ‚ä¶ z : A(id, 0)) (Œì(‚Üë) (x : ‚Ñï) (h : A(‚Üë)) ‚ä¶ s : A(‚Üë)(‚Üë¬≤, S x‚ÇÅ)) Œ£‚ÇÅ(‚Üë¬≤, ‚Ñï-elim A(‚Üë¬≤)[‚Üë¬≤, x‚ÇÄ] z‚ÇÅ[‚Üë] s‚ÇÄ[‚Üë] x‚ÇÄ) ‚áí Œ£‚ÇÄ (Œì (x : ‚Ñï) ‚ä¶ Œ± : A) Œ£‚ÇÅ
  (sig0, ElemEntry ctx ty, sig1) <- splitByVarName target alpha
    | _ => throw "\{alpha} is not an 'elem' entry"
  let Ext gamma x lastTy = ctx
    | _ => throw "Invalid context (1)"
  let NatTy = runSubst lastTy
    | _ => throw "Invalid context (2)"
  let sig = sig0 :< (z, ElemEntry gamma (ContextSubstElim ty (Ext Id NatVal0)))
                 :< (s, ElemEntry (Ext (Ext (subst gamma Wk) x NatTy) h (SignatureSubstElim ty Wk)) (ContextSubstElim (SignatureSubstElim ty Wk) (Ext (WkN 2) (NatVal1 (VarN 1)))))
  let sig = sig ++ subst sig1 (Ext (WkN 2)
                    (ElemEntryInstance $
                      NatElim x
                              (ContextSubstElim (SignatureSubstElim ty (Chain Wk Wk)) (Ext (WkN 2) (VarN 0)))
                              (SignatureVarElim 1 Wk)
                              x
                              h
                              -- Œì (x : ‚Ñï) (h : A) ‚ä¶ s
                              -- Œì (x : ‚Ñï) ‚ä¶ ‚Ñï _ _ x.h.s(‚Üë‚Å∫‚Å∫)
                              (SignatureVarElim 0 (UnderN 2 Wk))
                              CtxVar
                    ))
  return sig
compute (xs :< _) DebugDropLast = FailSt.do
  return xs
compute [<] DebugDropLast = FailSt.do
  throw "debug-drop-last: Signature is empty"


public export
computeN : Signature -> List Transformation -> M Signature
computeN sig [] = return sig
computeN sig (x :: xs) = computeN !(compute sig x) xs

public export
id : Rule Transformation
id = delim_ "id" $> Id

public export
wkCtx : Rule Transformation
wkCtx = do
  delim_ "wk-ctx"
  spaceDelim
  v <- varName
  pure (WkCtx v)

public export
wkTypE : Rule Transformation
wkTypE = do
  delim_ "wk-type"
  spaceDelim
  v0 <- varName
  spaceDelim
  v1 <- varName
  pure (WkTypE v0 v1)

public export
wkElem : Rule Transformation
wkElem = do
  delim_ "wk-elem"
  spaceDelim
  v0 <- varName
  spaceDelim
  v1 <- varName
  spaceDelim
  v2 <- varName
  pure (WkElem v0 v1 v2)

public export
wkLetElem : Rule Transformation
wkLetElem = do
  delim_ "wk-let-elem"
  spaceDelim
  v0 <- varName
  spaceDelim
  v1 <- varName
  spaceDelim
  v2 <- varName
  spaceDelim
  v3 <- varName
  pure (WkLetElem v0 v1 v2 v3)

public export
instCtxEmpty : Rule Transformation
instCtxEmpty = do
  delim_ "inst-ctx-empty"
  spaceDelim
  x <- varName
  pure (InstCtxEmpty x)

public export
instCtxCons : Rule Transformation
instCtxCons = do
  delim_ "inst-ctx-cons"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  spaceDelim
  w <- varName
  pure (InstCtxCons x y z w)

public export
instTypEExp : Rule Transformation
instTypEExp = do
  delim_ "inst-exp"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  pure (InstTypEExp x y z)

public export
instTypEPi : Rule Transformation
instTypEPi = do
  delim_ "inst-pi"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  spaceDelim
  w <- varName
  pure (InstTypEPi x y z w)

public export
instTypENat : Rule Transformation
instTypENat = do
  delim_ "inst-nat"
  spaceDelim
  x <- varName
  pure (InstTypENat x)

public export
instTypEUniverse : Rule Transformation
instTypEUniverse = do
  delim_ "inst-universe"
  spaceDelim
  x <- varName
  pure (InstTypEUniverse x)

public export
instTypEEl : Rule Transformation
instTypEEl = do
  delim_ "inst-el"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  pure (InstTypEEl x y)

public export
instElemLam : Rule Transformation
instElemLam = do
  delim_ "inst-lam"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  pure (InstElemLam x y)

public export
instTypEEq : Rule Transformation
instTypEEq = do
  delim_ "inst-eq"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  spaceDelim
  w <- varName
  pure (InstTypEEq x y z w)

public export
instContextVar : Rule Transformation
instContextVar = do
  delim_ "inst-ctx-var"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  pure (InstContextVar x y)

public export
renameSignatureVar : Rule Transformation
renameSignatureVar = do
  delim_ "rename-sig-var"
  spaceDelim
  x <- nat
  spaceDelim
  y <- varName
  pure (RenameSigVar x y)

public export
renameContextVar : Rule Transformation
renameContextVar = do
  delim_ "rename-ctx-var"
  spaceDelim
  x <- nat
  spaceDelim
  y <- nat
  spaceDelim
  z <- varName
  pure (RenameCtxVar x y z)

public export
instPiElim : Rule Transformation
instPiElim = do
  delim_ "inst-pi-elim"
  spaceDelim
  n <- varName
  spaceDelim
  dom <- varName
  spaceDelim
  x <- varName
  spaceDelim
  cod <- varName
  spaceDelim
  f <- varName
  spaceDelim
  e <- varName
  spaceDelim
  eq <- varName
  pure (InstPiElim n dom x cod f e eq)

public export
instTyRefl : Rule Transformation
instTyRefl = do
  delim_ "inst-ty-refl"
  spaceDelim
  n <- varName
  pure (InstTyRefl n)

public export
funExt : Rule Transformation
funExt = do
  delim_ "fun-ext"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  pure (FunExt x y z)

public export
instNatZero : Rule Transformation
instNatZero = do
  delim_ "inst-nat-0"
  spaceDelim
  x <- varName
  pure (InstNatZero x)

public export
instNatSuc : Rule Transformation
instNatSuc = do
  delim_ "inst-nat-suc"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  pure (InstNatSuc x y)

public export
instNatElim : Rule Transformation
instNatElim = do
  delim_ "inst-nat-elim"
  spaceDelim
  x <- varName
  spaceDelim
  y <- varName
  spaceDelim
  z <- varName
  spaceDelim
  w <- varName
  pure (InstNatElim x y z w)

public export
instSurfaceType : Rule Transformation
instSurfaceType = do
  delim_ "inst-surface-type"
  spaceDelim
  x <- varName
  spaceDelim
  tm <- term 0
  pure (InstSurfaceType x tm)

public export
instSurfaceElem : Rule Transformation
instSurfaceElem = do
  delim_ "inst-surface-elem"
  spaceDelim
  x <- varName
  spaceDelim
  tm <- term 0
  pure (InstSurfaceElem x tm)

public export
debugDropLast : Rule Transformation
debugDropLast = do
  delim_ "debug-drop-last"
  pure DebugDropLast

public export
transformation : Rule Transformation
transformation = id
             <|> wkCtx
             <|> wkTypE
             <|> wkElem
             <|> wkLetElem
             <|> instCtxEmpty
             <|> instCtxCons
             <|> instTypEExp
             <|> instTypEPi
             <|> instTypENat
             <|> instTypEUniverse
             <|> instTypEEl
             <|> instElemLam
             <|> instTypEEq
             <|> instContextVar
             <|> instNatElim
             <|> instNatZero
             <|> instNatSuc
             <|> renameSignatureVar
             <|> renameContextVar
             <|> instPiElim
             <|> instTyRefl
             <|> instSurfaceType
             <|> instSurfaceElem
             <|> funExt
             <|> debugDropLast
